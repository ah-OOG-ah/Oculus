plugins {
	id 'dev.architectury.loom' version '0.10.0-SNAPSHOT'
	id 'maven-publish'
}

loom {
	silentMojangMappingsLicense()

	forge {
		mixinConfigs = [
				"mixins.oculus.json",
				"mixins.oculus.fantastic.json",
				"mixins.oculus.vertexformat.json",
				"mixins.oculus.bettermipmaps.json",
				"mixins.oculus.fixes.maxfpscrash.json",
				"mixins.oculus.optimized-stitching.json",
				"oculus-batched-entity-rendering.mixins.json",
				"mixins.oculus.compat.sodium.json",
				"mixins.oculus.compat.json"
		]
	}
	mixin.defaultRefmapName = "oculus-mixins-refmap.json"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
	maven {
		name = "CurseForge"
		url = "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

configurations {
	toJar
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	compileOnly "org.apache.ant:ant:1.8.2"
	modCompileOnly "maven.modrinth:rubidium:0.2.13"
	//modCompileOnly "me.jellysquid.mods:Rubidium:0.2.13"
	modCompileOnly "curse.maven:epic-fight-mod-405076:4029362"
	modCompileOnly "curse.maven:pixelmon-389487:4718641"

	implementation fileTree(include: ['*.jar'], dir: 'libs')
	toJar fileTree(include: ['antlr4-runtime-4.9.1.jar', 'glsl-transformer-1.0.0-pre21.2.jar'], dir: 'libs')
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

java {
	withSourcesJar()
}

jar {
	from { configurations.toJar.collect { it.isDirectory() ? it : zipTree(it) } }
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
	}
}
